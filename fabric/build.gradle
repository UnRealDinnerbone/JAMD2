plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

architectury {
    platformSetupLoomIde()
    fabric()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${parent.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${parent.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "me.shedaniel:architectury-fabric:${parent.architectury_version}"
    modCompile "me.shedaniel.cloth:cloth-config-fabric:${parent.cloth_config_version}"

    implementation(project(path: parent.getProjectName("common"))) {
        transitive = false
    }
    developmentFabric(project(path: parent.getProjectName("common"))) {
        transitive = false
    }
    shadowCommon(project(path: parent.getProjectName("common"), configuration: "transformProductionFabric")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"
}

jar {
    classifier "dev"
}

java {
    withSourcesJar()
}

sourcesJar {
    def commonSources = project(parent.getProjectName("common")).remapSourcesJar
    dependsOn commonSources
    from zipTree(commonSources.output)
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = project.archives_base_name + "-" + project.name
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                classifier null
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
